buildscript {
	ext {
		// TODO: update version to 2+ (fix compatibility issues with spring-cloud)
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3.1"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.dorongold.task-tree"

group = 'com.zuehlke'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR1'
    }
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'io.springfox:springfox-swagger2:2.6.1'
	compile 'io.springfox:springfox-swagger-ui:2.6.1'
	compile 'org.springframework.cloud:spring-cloud-starter-feign'
	compile 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
	compile 'org.springframework.cloud:spring-cloud-starter-hystrix-dashboard'
	compile 'io.github.openfeign:feign-jackson:9.5.0'

	// dependencies for testing
	testCompile 'io.rest-assured:rest-assured:3.0.2'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def webappDir = "$projectDir/src/main/webapp"

// gets called from the java plugin build task
processResources {
	dependsOn "copyFrontendDist"
}

// TODO: check if there is a more elegant way to build the frontend here
// copies the built frontend content into the static resources directory
task copyFrontendDist(type: Copy) {
	dependsOn "buildFrontend"

	from "$webappDir/dist/webapp"
	into "$projectDir/src/main/resources/static"
}

// builds the frontend webapp into src/main/webapp/dist
task buildFrontend(type:Exec) {
	dependsOn "installFrontend"

	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
	commandLine "npm", "run", "ng", "build"
}

// runs the frontend tests
task testFrontend(type:Exec) {
    dependsOn "installFrontend"

    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "npm", "run", "test-headless"
}

// installs the client webapp
task installFrontend(type:Exec) {
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
    commandLine "npm", "install"
}